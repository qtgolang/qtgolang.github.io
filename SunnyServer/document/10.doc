.版本 2

.子程序 __启动窗口_创建完毕


' Sunny服务器套件 在模块中 已是全局变量 ,导入模块 可直接使用

' 添加一个通用路由,匹配所有HTTP方法的路由
' 如 GET, POST, PUT, PATCH, HEAD, OPTIONS, DELETE, CONNECT, TRACE.

' 该示例访问地址为 http://127.0.0.1:80/restfull/999999999?name=sunny
Sunny服务器套件.添加通用路由 (“/restfull/:name”, 到整数 (&获取请求参数))
' :80代表启动的服务器端口为80 
.如果 (Sunny服务器套件.启动HTTP服务器 (“:80”))
    信息框 (“服务器启动成功”, 0, , )
.否则
    信息框 (Sunny服务器套件.获取运行时的错误信息 (), #错误图标, “启动失败”, )
.如果结束


.子程序 获取请求参数
.参数 Context
.局部变量 SUNNY, Sunny
.局部变量 请求, Sunny请求
.局部变量 响应, Sunny响应

' 必须使用该语句取回sunny对象
SUNNY ＝ 取回sunny对象 (Context, 请求, 响应)
' 200返回的状态码  第二个参数是返回给浏览器的数据

' ======================================================================
' 取URL 变量参数 (RestFull 风格)
' 例如 请求地址为 http://127.0.0.1:80/restfull/999999999?name=sunny
调试输出 (请求.取参数Param (“name”))  ' 得到 【999999999】
' ======================================================================
' 取GET参数
' 例如 请求地址为 http://127.0.0.1:80/restfull/999999999?name=sunny
调试输出 (请求.取GET参数 (“name”))  ' 得到 【sunny】
调试输出 (请求.取GET参数_有默认值 (“name1”, “no”))  ' 得到 【no】
' ======================================================================
' 取POST参数
' 例如 请求地址为 http://127.0.0.1:80/restfull/999999999?name=sunny
' POST内容 a=b&b=c&c=100&name=sunny2021
调试输出 (请求.取POST参数 (“name”))  ' 得到 【sunny2021】
' ======================================================================
' 取POST原始数据
' 例如 请求地址为 http://127.0.0.1:80/restfull/999999999?name=sunny
' POST内容 a=b&b=c&c=100&name=sunny2021
调试输出 (请求.取Post原始内容_GBK ())  ' 得到 【a=b&b=c&c=100&name=sunny2021】
' 其他相关命令
' 请求.取Post原始内容_GBK ()
' 请求.取Post原始内容_UTF8 ()
' 请求.取Post原始内容_字节集 ()
' 请求.取Post原始内容_指针 ()
' 请求.取Post原始内容_长度 ()
' ======================================================================
' 模糊取参数
' 例如 请求地址为 http://127.0.0.1:80/restfull/999999999?name=sunny&name1=sunnyget
' POST内容 a=b&b=c&c=100&name=sunny2021&name1=sunnyget&name2=sunnypost
调试输出 (请求.取参数 (“name”))  ' 得到 【999999999】
调试输出 (请求.取参数 (“name1”))  ' 得到 【sunnyget】
调试输出 (请求.取参数 (“name2”))  ' 得到 【sunnypost】
' 优先取URL变量，其次GET ,最后POST
响应.写出文本 (200, “获取参数：id=” ＋ 请求.取参数 (“name”))