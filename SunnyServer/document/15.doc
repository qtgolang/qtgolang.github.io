.版本 2
.支持库 spec

.子程序 __启动窗口_创建完毕

' 添加一个 GET 路由，只能使用 GET 请求，其他请求不会回调
' 该示例访问地址为 ws://127.0.0.1/ws
' 测试步骤 打开 http://coolaf.com/tool/chattest
' 在网页中输入 ws://127.0.0.1/ws

' Sunny服务器套件 在模块中 已是全局变量 ,导入模块 可直接使用
Sunny服务器套件.添加GET路由 (“/ws”, 到整数 (&ws))

' 如需 创建wss，只需启动HTTPS服务器 即可

' :80代表启动的服务器端口为80
.如果 (Sunny服务器套件.启动HTTP服务器 (“:80”))
    信息框 (“服务器启动成功”, 0, , )
.否则
    信息框 (Sunny服务器套件.获取运行时的错误信息 (), #错误图标, “启动失败”, )
.如果结束


.子程序 ws
.参数 Context
.局部变量 SUNNY, Sunny
.局部变量 请求, Sunny请求
.局部变量 响应, Sunny响应
.局部变量 WsContext, 整数型

' 必须使用该语句取回sunny对象
SUNNY ＝ 取回sunny对象 (Context, 请求, 响应)

' 检查协议头 判断是否为 websocket 请求
.如果真 (到小写 (SUNNY.请求.取一个协议头 (“Upgrade”)) ≠ “websocket”)
    SUNNY.响应.写出文本 (200, “请求协议错误”)
    返回 ()
.如果真结束

' bin = 升级成功后,立即给客户端发送的消息
' 请注意编码，如果编码不对，可能导致连接断开


' 绑定Ws消息回调函数地址
' 回调函数(&Ws消息子程序)需要2个整数型参数
' 第一个参数为 WsContext
' 第二个参数为MsgType[1=正常接收消息 -1=断开]。

WsContext ＝ 请求.升级至WS路由 (到整数 (&Ws消息子程序))


' WsContext 升级成功后 可保存 WsContext 可用于后续主动给客户发送消息
' Sunny服务器套件.Ws_给指定客户端发送消息 (WsContext, 到字节集 (“123456789”))
' ================================================
' 全局的相关命令 可在任意代码处调用
' Sunny服务器套件.Ws_取已连接数量 ()
' Sunny服务器套件.Ws_断开指定客户端 ()
' Sunny服务器套件.ws_取全部在线客户端 ()
' Sunny服务器套件.Ws_获取客户端IP地址 ()
' Sunny服务器套件.Ws_发送给全部在线客户端 ()
' Sunny服务器套件.ws_取全部在线客户端String ()
' Sunny服务器套件.Ws_给指定客户端发送消息 ()
' ================================================

.子程序 Ws消息子程序
.参数 WsContext
.参数 MsgType, , , [1=正常接收消息 -1=断开]。
.局部变量 Sunny, Sunny
.局部变量 ws, SunnyWs
.局部变量 bin, 字节集

.如果真 (MsgType ＝ -1)
    调试输出 (“Ws 客户”, WsContext, “连接断开”)
    返回 ()
.如果真结束

' 必须使用该语句取回对象
Sunny ＝ 取回sunny对象 (WsContext)  ' 这里是 获取请求连接时的数据，不获取的话，可删
调试输出 (Sunny.请求.取URL ())

' 这里取ws客户相关信息
' 必须使用该语句取回对象
Sunny.Ws.置WsContext (WsContext)

' ================================================
' 相关命令
' Sunny.Ws.取消息_字节集 ()
' Sunny.Ws.取消息_UTF8 ()
' Sunny.Ws.取消息_GBK ()

' Sunny.Ws.取消息_指针 ()
' Sunny.Ws.取消息长度 ()

' Sunny.Ws.发送字节集 ()
' Sunny.Ws.发送字符串_GBK ()
' Sunny.Ws.发送字符串_UTF8 ()
' ================================================
调试输出 (Sunny.Ws.取消息类型 (), Sunny.Ws.取消息_UTF8 ())
Sunny.Ws.发送字符串_UTF8 (“你发送的消息是：” ＋ Sunny.Ws.取消息_UTF8 ())