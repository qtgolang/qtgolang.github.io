.版本 2
.支持库 spec

.子程序 __启动窗口_创建完毕

' 该示例访问地址为
' http://127.0.0.1/index
' http://127.0.0.1/111111111111

' Sunny服务器套件 在模块中 已是全局变量 ,导入模块 可直接使用

' 添加两个 全局 路由，使用任何请求都会触发
' 越写在前面的函数 会越先执行
Sunny服务器套件.添加全局函数 (到整数 (&全局函数1))
Sunny服务器套件.添加全局函数 (到整数 (&全局函数2))
' 全局函数 不要写到方法函数之后
Sunny服务器套件.添加GET路由 (“/index”, 到整数 (&index))
' :80代表启动的服务器端口尾80 
.如果 (Sunny服务器套件.启动HTTP服务器 (“:80”))
    信息框 (“服务器启动成功”, 0, , )
.否则
    信息框 (Sunny服务器套件.获取运行时的错误信息 (), #错误图标, “启动失败”, )
.如果结束


.子程序 全局函数1
.参数 Context
.局部变量 SUNNY, Sunny
.局部变量 请求, Sunny请求
.局部变量 响应, Sunny响应
.局部变量 已登录, 逻辑型, , , 鉴权

' 必须使用该语句取回sunny对象
SUNNY ＝ 取回sunny对象 (Context, 请求, 响应)
调试输出 (“全局函数1,被执行了”)
' ====================================================================
' 鉴权操作 或其他形式鉴权
.如果真 (请求.取指定Cookie (“login”) ＝ “”)
    已登录 ＝ 真  ' 假设已经登录
.如果真结束
' ====================================================================

.判断开始 (已登录)
    ' 继续处理之后的函数 会提前执行 全局函数2 index 函数
    ' 否则,本函数（全局函数1）才会执行 全局函数2 index 函数
    响应.继续处理之后的函数 ()
    ' 执行完成后，将不会再次执行  全局函数2 index 函数
    ' 这时候可以获取到其他返回返回的状态码 和返回的字节长度
    ' 响应.取响应状态码 ()
    ' 响应.取已写入的字节数 ()

.默认
    ' 不再执行其他剩余函数 将不会执行 全局函数2 index 函数
    响应.不再执行其他剩余函数 ()
.判断结束



.子程序 全局函数2
.参数 Context
.局部变量 SUNNY, Sunny
.局部变量 请求, Sunny请求
.局部变量 响应, Sunny响应

' 必须使用该语句取回sunny对象
SUNNY ＝ 取回sunny对象 (Context, 请求, 响应)

调试输出 (“全局函数2,被执行了”)

响应.写出文本 (200, “这是 全局函数2 返回的数据”)


.子程序 index
.参数 Context
.局部变量 SUNNY, Sunny
.局部变量 请求, Sunny请求
.局部变量 响应, Sunny响应

' 必须使用该语句取回sunny对象
SUNNY ＝ 取回sunny对象 (Context, 请求, 响应)
调试输出 (“方法函数 被执行了”)

响应.写出文本 (200, “这是 方法函数 返回的数据”)